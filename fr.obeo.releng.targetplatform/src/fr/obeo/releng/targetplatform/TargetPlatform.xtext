grammar fr.obeo.releng.targetplatform.TargetPlatform with org.eclipse.xtext.common.Terminals

import "fr.obeo.releng.targetplatform"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
											
TargetPlatform:
	(
		'target' name=STRING
		contents+=TargetContent*
	)?
;

TargetContent: 
	Options | Environment | IncludeDeclaration | Location
;

Options: 
	'with' options+=Option (',' options+=Option)*
;

Environment:
	'operatingSystem' operatingSystem=ID & 
	'windowingSystem' windowingSystem=ID & 
	'architecture' architecture=ID & 
	'localization' localization=Locale &
	'executionEnvironment' executionEnvironment=ExecutionEnvironment
;

IncludeDeclaration:
	'include' importURI=STRING
;

Location:
	'location' ((ID=ID)? & uri=STRING) ('{'
		('with' options+=Option (',' options+=Option)*)?
		ius+=IU*
	'}')?
;

enum Option:
	INCLUDE_REQUIRED |
	INCLUDE_ALL_ENVIRONMENTS |
	INCLUDE_SOURCE |
	INCLUDE_CONFIGURE_PHASE
;

IU:
	ID=QualifiedName ( ';' 'version' '=' (version=VersionRange|version=STRING) )? 
;

QualifiedName hidden ():
	ID (=> '.' ID)*
;

Version hidden():
	INT ('.' INT ('.' INT ('.' (ID|INT))?)?)?
;

VersionRange hidden (WS):
	( ('('|'[') Version ',' Version (')'|']') ) | Version | 'lazy'
;

Locale returns Locale hidden():
	ID
;

ExecutionEnvironment returns ExecutionEnvironment hidden():
	ID
;

terminal INT returns ecore::EInt: ('0'..'9')+;
terminal ID: '^'?('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')+;