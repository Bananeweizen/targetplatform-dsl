grammar fr.obeo.releng.targetplatform.TargetPlatform hidden(WS, ML_COMMENT, SL_COMMENT)

generate targetplatform "http://www.obeo.fr/releng/TargetPlatform"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
											
TargetPlatform:
	(
		'target' name=STRING
		(
			('version'  targetVersions+=TargetVersion (',' targetVersions+=TargetVersion)*)?
			& ('with' options+=Option (',' options+=Option)*)?	
		)
		
		locations+=Location*
	)?
;
	
enum TargetVersion:
	TARGET_38='PDE_3.8' | TARGET_36='PDE_3.6'
;

Location:
	'location' ((id=ID)? & uri=STRING) ('{'
		('with' options+=Option (',' options+=Option)*)?
		ius+=IU*
	'}')?
;

enum Option:
	INCLUDE_REQUIRED='requirements' |
	INCLUDE_ALL_ENVIRONMENTS='allEnvironments' |
	INCLUDE_SOURCE='source' |
	INCLUDE_CONFIGURE_PHASE='configurePhase'
;

IU:
	ID=QualifiedName ( ';' 'version' '=' (version=VersionRange|version=STRING) )? 
;

QualifiedName hidden ():
	ID (=> '.' ID)*
;

Version hidden():
	INT ('.' INT ('.' INT ('.' (ID|INT))?)?)?
;

VersionRange hidden ():
	( ('('|'[') WS* Version WS* ',' WS* Version WS*  (')'|']') )
	|
	Version
;

terminal INT returns ecore::EInt: ('0'..'9')+;
terminal ID: '^'?('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')+;

terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
